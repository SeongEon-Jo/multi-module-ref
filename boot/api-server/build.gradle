plugins {
	id "org.asciidoctor.convert" version "1.5.10"
}

repositories {
	mavenCentral()
}

bootJar {
	enabled = true
}

jar {
	enabled = true
}

sourceSets {
	acceptanceTest {
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}

	documentation {
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}
}

configurations {
	// acceptanceTest
	acceptanceTestImplementation.extendsFrom(implementation, testImplementation)
	acceptanceTestRuntimeOnly.extendsFrom(runtimeOnly, testRuntimeOnly)
	acceptanceTestImplementation.canBeResolved(true)

	// documentation
	documentationImplementation.extendsFrom(implementation, testImplementation)
	documentationRuntimeOnly.extendsFrom(runtimeOnly, testRuntimeOnly)
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// rest-assured
	acceptanceTestImplementation 'io.rest-assured:rest-assured'
	documentationImplementation 'io.rest-assured:rest-assured:3.3.0'
	documentationImplementation 'io.rest-assured:spring-mock-mvc:3.3.0'

	// rest-docs
	documentationImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	documentationImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

	// cucumber
	acceptanceTestImplementation 'io.cucumber:cucumber-java:6.10.4'
	acceptanceTestImplementation 'io.cucumber:cucumber-spring:6.10.4'
	acceptanceTestImplementation 'io.cucumber:cucumber-junit-platform-engine:6.10.4'
}

task acceptanceTest(type: Test) {
	testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
	classpath = sourceSets.acceptanceTest.runtimeClasspath
	useJUnitPlatform()
}

task documentationTest(type: Test) {
	testClassesDirs = sourceSets.documentation.output.classesDirs
	classpath = sourceSets.documentation.runtimeClasspath
	useJUnitPlatform()
}

ext {
	snippetsDir = file('build/generated-snippets')
}

asciidoctor {
	inputs.dir snippetsDir
	sourceDir 'src/documentation/adoc'
	dependsOn documentationTest
}